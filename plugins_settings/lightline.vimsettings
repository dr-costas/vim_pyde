" ~/.myvim_files/plugins_settings/lightline.vimsettings file starts
"
" ==================================
" Lightline settings file
" ==================================
"
" Set last status to 2 so Lightline can work properly
set laststatus=2
"
" Set the options for lightline
let g:lightline = {
	\ 'colorscheme': 'OldHope',
	\ 'active': {
	\   'left': [ [ 'mode', 'paste', 'readonly' ], 
	\			  [ 'fugitive' ],
    \             [ 'filename', 'separator', 'modified' ]
	\		    ],
    \   'right': [ [ 'lineinfo' ],
    \              [ 'percent' ],
    \              [ 'fileformat', 'fileencoding', 'filetype', ] ]
	\ },
	\ 'inactive': {
	\   'left': [ [ 'fugitive' ],
    \             [ 'filename', 'separator', 'modified' ]
	\		    ],
    \   'right': [ [ 'lineinfo' ],
    \              [ 'filetype', ] ]
	\ },
    \ 'tabline': {
    \   'left': [ [ 'bufferinfo' ],
    \             [ 'separator' ],
    \             [ 'bufferbefore', 'buffercurrent', 'bufferafter' ], ],
    \   'right': [ [ 'clock' ], ],
    \ },
	\ 'component_expand': {
    \   'buffercurrent': 'lightline#buffer#buffercurrent',
    \   'bufferbefore': 'lightline#buffer#bufferbefore',
    \   'bufferafter': 'lightline#buffer#bufferafter',
    \ },
	\ 'component_function': {
	\   'fugitive': 'LightlineFugitive',
	\   'filename': 'LightlineFilename',
    \   'bufferinfo': 'lightline#buffer#bufferinfo',
	\	'clock': 'ShowTime',
	\ },
    \ 'component_type': {
    \   'buffercurrent': 'tabsel',
    \   'bufferbefore': 'raw',
    \   'bufferafter': 'raw',
    \ }
	\ }
" 'right': [ [ 'clock', 'close' ], ],
" ----------------------------------------------------------
" Functions
" ----------------------------------------------------------
"
" Function to check if the file has been modified
function! LightlineModified()
	return &ft =~# 'help\|vimfiler' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction
"
" Function to check the read-only property
function! LightlineReadonly()
	return &ft !~? 'help\|vimfiler' && &readonly ? 'RO' : ''
endfunction
"
" Function to get the current branch from Fugitive
function! LightlineFugitive()
	if &ft !~? 'vimfiler' && exists('*FugitiveHead')
		return FugitiveHead()
	endif
	return ''
endfunction
" 
" Function to use the path of a file relative to current  dir.
function! LightlineFilename()
  let root = fnamemodify(get(b:, 'git_dir'), ':h')
  let path = expand('%:p')
  if path[:len(root)-1] ==# root
    return path[len(root)+1:]
  endif
  return expand('%')
endfunction
"
function! ShowTime()
	return strftime('%c')
endfunction
"
function! UpdateStatus(timer)
	redrawtabline
endfunction
"
let statusTimer = timer_start(1000, 'UpdateStatus', {'repeat': -1})
"
command LightlineReload call LightlineReload()

function! LightlineReload()
	call lightline#init()
	call lightline#colorscheme()
	call lightline#update()
endfunction
"
" EOF
